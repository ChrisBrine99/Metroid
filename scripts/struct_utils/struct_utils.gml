// A variable that uses an ID'ing system that works very similarly to how GameMaker's own instance ID system
// functions; giving each new instance of a struct (So long as they inherit from "base_struct" found below) a
// unique number that it can be identified with.
global.singletonID = 1000000;

/// @description A basic struct that all others must inherit from (So long as they use the "constructor" moniker
/// and aren't created and assigned to local variables within an object/struct upon said object/struct's creation).
/// This means all structs will have their own initialize and cleanup functions regardless of if they inherit
/// them for their own use or not.
/// @param {Real} index		Unique value generated by GML during compilation that represents this struct asset.
function base_struct(_index) constructor{
	id = global.singletonID;
	object_index = _index;

	static initialize = function(){}
	static cleanup = function(){}
}

/// @description Creates a new struct object in a way that mimics how GameMaker creates and handles its own
/// object instances; storing a unique id indentifier value to a struct upon creation for easy reference of
/// a single instance or instances of that struct. Singleton structs will not be able to be created by this
/// function if an instance of them already exists in the game.
/// @param {Function}	struct	The struct object to be instantiated by GameMaker.
function instance_create_struct(_struct){
	if (!singleton_instance_exists(_struct)){
		var _instance = new _struct(_struct);
		_instance.initialize(); // Every instance overrides this function from the base struct.
		global.singletonID++;
		return _instance;
	}
	return noone;
}

/// @description Creates a struct that is deemed a singleton, which means calling this function again in an
/// attempt to intialize another instance of this struct will cause it to create nothing; the same applying
/// to "instance_create_struct" if used instead.
/// @param {Function}	struct	The struct object that will be created and assigned as a singleton to prevent its duplication.
function instance_create_singleton_struct(_struct){
	var _instance = instance_create_struct(_struct);
	if (_instance != noone) {ds_map_add(global.sInstances, _struct, _instance);}
}

/// @description A function that mimics how GameMaker's own "instance_destroy" function works, but focuses on
/// the deletion of structs that exist within the game currently. Singleton structs are not allowed to be
/// deleted by this function; as they generally exist for the entire duration of the game's runtime.
/// @param {Function}	instance	The struct instance that will be marked for deletion by GameMaker's garbage collector.
function instance_destroy_struct(_instance){
	if (singleton_instance_exists(_instance.object_index)) {return;}
	_instance.cleanup();
	delete _instance;
}